buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }

    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
    }
}

plugins {
    id "com.matthewprenger.cursegradle" version "1.1.0"
    id "se.bjurr.gitchangelog.git-changelog-gradle-plugin" version "1.64"
}


apply plugin: "net.minecraftforge.gradle.forge"


repositories {
    mavenCentral()
    maven {
        name = "The One Probe"
        url = "https://maven.tterrag.com"
    }
    maven {
        name = "CoFH"
        url = "http://maven.covers1624.net/"
    }
    maven {
        name = "ChickenBones"
        url = "http://chickenbones.net/maven"
    }
    maven {
        name = "Tinkers', JEI"
        url = "http://dvs1.progwml6.com/files/maven"
    }
    maven {
        name = "OpenComputers"
        url = "http://maven.cil.li/"
    }
    maven {
        name = "CurseForge"
        url = "https://minecraft.curseforge.com/api/maven/"
    }
    maven {
        url = "https://www.cursemaven.com"
    }
}

configurations.all {
    exclude group: "mezz.jei", module: "jei_1.12.2"
}

dependencies {
    compile files("E:/.minecraft/realmofstorms/source files/TheOneProbe-1.12/build/libs/theoneprobe-1.12-1.4.28.jar")
//    compileOnly("mcjty.theoneprobe:TheOneProbe-1.12:1.12-1.4.22-13") { transitive = false }

    deobfCompile("curse.maven:industrial-craft-242638:3838713")

    deobfCompile "li.cil.oc:OpenComputers:MC1.12.1-1.7.+"

    compileOnly("curse.maven:buildcraft-61811:3204475")

    deobfCompile("curse.maven:forestry-59751:2918418")
    deobfCompile("curse.maven:binnie-mods-223525:2916129") { transitive = false }

    deobfCompile "slimeknights.mantle:Mantle:1.12-1.3.3.55"
    deobfCompile("slimeknights:TConstruct:1.12.2-2.13.0.179") { transitive = false }

    runtime "curse.maven:guideapi-228832:2645992"
    deobfCompile "curse.maven:bloodmagic-224791:2822288"

    runtime "team.chisel.ctm:CTM:MC1.12.2-0.3.3.22"
    deobfCompile "chisel:Chisel:MC1.12.2:1.0.1.44"

    compile "chameleon:Chameleon:1.12:4.1.3"
    deobfCompile "storage-drawers:StorageDrawers:1.12.2:5.4.0"

    runtime "codechicken:CodeChickenLib:1.12.2-3.2+:universal"
    runtime "cofh:CoFHWorld:1.12.2-1.2+:universal"
    runtime "cofh:ThermalFoundation:1.12.2-2.6.3.27:universal"
    deobfCompile "cofh:ThermalExpansion:1.12.2-5.5.4.43:universal"
    deobfCompile "cofh:ThermalDynamics:1.12.2-2.5.5.21:universal"
    deobfCompile "cofh:CoFHCore:1.12.2-4.6.3.27:universal"
    deobfCompile "cofh:RedstoneFlux:1.12-2.1.0.+:universal"

    runtime "curse.maven:enderio1-64578:3328811"
    runtime "curse.maven:endercore1-231868:2972849"

    compileOnly fileTree(dir: 'libs', include: ['*.jar'])
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

version = mc_version + '-' + MAJOR + '.' + MINOR + '.' + PATCH
group = "io.github.drmanganese"
archivesBaseName = "topaddons"

minecraft {
    version = mc_version + '-' + forge_version
    runDir = "run"
    mappings = mcp_mappings

    replace '@VERSION@', project.version
    useDepAts = true
}


processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {
    from "LICENSE"
}

task generateGitChangelog(type: se.bjurr.gitchangelog.plugin.gradle.GitChangelogTask) {
    file = new File("CHANGELOG.md")
    fromRef = "v1.11.2-0.15.0"
    toRef = "HEAD"
    templateContent = file('changelog.mustache').getText('UTF-8')
    ignoreCommitsIfMessageMatches = "v.*-.*|Merge"
}

curseforge {
    project {
        id = "247111"
        changelogType = "markdown"
        changelog = file("CHANGELOG.md")
        releaseType = "release"
        mainArtifact jar
        addArtifact sourceJar
        relations {
            requiredLibrary "the-one-probe"
        }
    }
}

sourceSets {
    main {
        java {
            exclude '**/addons/AddonNeoTech.java'
        }
    }
}

idea { module { inheritOutputDirs = true } }
